#****************************************************************************
# Copyright (C) 2018 pmdtechnologies ag & Infineon Technologies
#
# THIS CODE AND INFORMATION ARE PROVIDED "AS IS" WITHOUT WARRANTY OF ANY
# KIND, EITHER EXPRESSED OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE
# IMPLIED WARRANTIES OF MERCHANTABILITY AND/OR FITNESS FOR A
# PARTICULAR PURPOSE.
#
#****************************************************************************

INCLUDE(InstallRequiredSystemLibraries)

SET(CPACK_PACKAGE_NAME "Royale")
SET(CPACK_PACKAGE_DESCRIPTION_SUMMARY "Royale")
SET(CPACK_PACKAGE_VENDOR "pmd technologies gmbh & Infineon Technologies AG")
SET(CPACK_PACKAGE_VERSION_MAJOR ${ROYALE_VERSION_MAJOR})
SET(CPACK_PACKAGE_VERSION_MINOR ${ROYALE_VERSION_MINOR})
SET(CPACK_PACKAGE_VERSION_PATCH ${ROYALE_VERSION_PATCH})

IF(${ROYALE_VERSION_BUILD})
    SET(CPACK_PACKAGE_VERSION_BUILD "${ROYALE_VERSION_BUILD}")
ELSE()
    SET(CPACK_PACKAGE_VERSION_BUILD "0")
ENDIF()

IF(ROYALE_CUSTOMER_SUFFIX)
    SET(CPACK_PACKAGE_VERSION "${CPACK_PACKAGE_VERSION_MAJOR}.${CPACK_PACKAGE_VERSION_MINOR}.${CPACK_PACKAGE_VERSION_PATCH}.${CPACK_PACKAGE_VERSION_BUILD}.${ROYALE_CUSTOMER_SUFFIX}")
ELSE()
    SET(CPACK_PACKAGE_VERSION "${CPACK_PACKAGE_VERSION_MAJOR}.${CPACK_PACKAGE_VERSION_MINOR}.${CPACK_PACKAGE_VERSION_PATCH}.${CPACK_PACKAGE_VERSION_BUILD}")
ENDIF()

SET(CPACK_PACKAGE_FILE_NAME "libroyale-${CPACK_PACKAGE_VERSION}-${ROYALE_TARGET_PLATFORM}-${ARCHITECTURE}-${ARCHITECTURE_BITNESS}Bit")
SET(CPACK_PACKAGE_INSTALL_DIRECTORY "Royale")
SET(CPACK_OUTPUT_FILE_PREFIX packages)

function(create_cpack_config fileConfig)
  set(CPACK_OUTPUT_CONFIG_FILE "${fileConfig}")
  include(CPack)
endfunction(create_cpack_config)

if(${ROYALE_TARGET_PLATFORM} STREQUAL WINDOWS)
    include("${CMAKE_CURRENT_LIST_DIR}/packaging_windows.cmake")
    create_cpack_config(${CMAKE_BINARY_DIR}/CPackConfig.cmake)
else()
    set(CPACK_GENERATOR "ZIP")
    create_cpack_config(${CMAKE_BINARY_DIR}/CPackConfig.cmake)
endif()

if(ROYALE_DEVELOPER_EDITION AND (${ROYALE_TARGET_PLATFORM} STREQUAL WINDOWS))
 set(CPACK_GENERATOR "ZIP")
 set(CPACK_ARCHIVE_COMPONENT_INSTALL ON)
 set(CPACK_COMPONENTS_GROUPING ALL_COMPONENTS_IN_ONE)
 set(CPACK_COMPONENTS_ALL Unspecified DevPack)
 set(CPACK_PACKAGE_FILE_NAME "libroyale-${CPACK_PACKAGE_VERSION}-${ROYALE_TARGET_PLATFORM}-${ARCHITECTURE}-${ARCHITECTURE_BITNESS}Bit(Dev-Edition)")
 create_cpack_config(${CMAKE_BINARY_DIR}/CPackConfigDev.cmake)
 add_custom_target(DEV_PACKAGE
            COMMAND "${CMAKE_CPACK_COMMAND}"
                     "-C" "$<CONFIGURATION>"
                "--config" "${CMAKE_BINARY_DIR}/CPackConfigDev.cmake"
            )
 set_target_properties(DEV_PACKAGE
    PROPERTIES
    FOLDER CMakePredefinedTargets
    )
endif()
