cmake_policy(SET CMP0046 NEW)

set (STORAGE_TEST_HEADERS
    "inc/MockBridgeImagerSpiStorage.hpp"
    "inc/MockRandomAccessStorage.hpp"
    "inc/StorageTestUtils.hpp"
    "inc/ZwetschgeTestUtils.hpp"
    )

set(STORAGE_TEST_SOURCES
    "src/MockBridgeImagerSpiStorage.cpp"
    "src/TestNonVolatileStoragePersistent.cpp"
    "src/TestNonVolatileStorageShadow.cpp"
    "src/TestStorageFormatPicoLegacy.cpp"
    "src/TestStorageFormatPolar.cpp"
    "src/TestStorageFormatZwetschge.cpp"
    "src/TestStorageI2cEeprom.cpp"
    "src/TestStorageSpiImagerM2452.cpp"
    "src/TestStorageSpiImagerM2453.cpp"
    "src/TestStorageSpiImagerM2455.cpp"
    "src/ZwetschgeTestUtils.cpp"
    )

# The TestNonVolatileStoragePersistent needs a file which it can read from, but the tests don't
# write to it.  The .cpp file itself is enough.
ADD_DEFINITIONS(-DTEST_FILE_FOR_NONVOLATILESTORAGEPERSISTENT="${CMAKE_CURRENT_SOURCE_DIR}/src/TestNonVolatileStoragePersistent.cpp")

# TestStorageSpiImagerM2453 depends on comptests_imager for SimImagerM2453 and SpiGenericFlash
# TestStorageFormatZwetschge depends on the ImagerLenaReader and sample Lena file
include_directories(
    ${gtest_SOURCE_DIR}/include
    ${CMAKE_CURRENT_SOURCE_DIR}/inc
    $<TARGET_PROPERTY:comptests_imager,INCLUDE_DIRECTORIES>
    $<TARGET_PROPERTY:test_framework_royalecore,INCLUDE_DIRECTORIES>
    )
add_library(comptests_storage OBJECT ${STORAGE_TEST_SOURCES} ${STORAGE_TEST_HEADERS})
set_target_properties(comptests_storage
    PROPERTIES
    FOLDER components/tests
    EXCLUDE_FROM_ALL true
    )

# The TestStorageFormatZwetschge needs a file which it can read from, which is generated by the
# generate_zwetschge_file target in components/storage/zwetschgeTool. A prebuilt copy is used for
# the generated_zwetschge_matches_example test, but the freshly-generated file is used for the C++
# tests because this will make it easier to update the format later (as we will for mixed mode).
set (exampleZwetschgeDevice "${ROYALE_RUNTIME_OUTPUT_DIRECTORY}/exampleDevice.zwetschge")
set (exampleZwetschgeFlashImage "${ROYALE_RUNTIME_OUTPUT_DIRECTORY}/exampleFlashImage.zwetschge")
ADD_DEFINITIONS(-DEXAMPLE_DEVICE_FOR_ZWETSCHGE="${exampleZwetschgeDevice}")
ADD_DEFINITIONS(-DEXAMPLE_FLASH_IMAGE_FOR_ZWETSCHGE="${exampleZwetschgeFlashImage}")
generate_zwetschge_file (exampleZwetschgeDeviceTarget
    OUTPUT "${ROYALE_RUNTIME_OUTPUT_DIRECTORY}/exampleDevice.zwetschge"
    MAIN_DEPENDENCY "${CMAKE_CURRENT_SOURCE_DIR}/data/zwetschgeExample/ExampleDevice.py"
    DEPENDS "${CMAKE_CURRENT_SOURCE_DIR}/data/zwetschgeExample/mode_9_50fps.py"
    )
generate_zwetschge_file (exampleZwetschgeFlashImageTarget
    OUTPUT "${ROYALE_RUNTIME_OUTPUT_DIRECTORY}/exampleFlashImage.zwetschge"
    MAIN_DEPENDENCY "${CMAKE_CURRENT_SOURCE_DIR}/data/zwetschgeExample/ExampleFlashImage.py"
    DEPENDS "${CMAKE_CURRENT_SOURCE_DIR}/data/zwetschgeExample/mode_9_50fps.py"
    )
add_test(NAME generated_zwetschge_matches_example
    COMMAND "${CMAKE_COMMAND}" -E compare_files "${CMAKE_CURRENT_SOURCE_DIR}/data/prebuilt_copy_of_exampleDevice.zwetschge" "${ROYALE_RUNTIME_OUTPUT_DIRECTORY}/exampleDevice.zwetschge"
    )
add_test(NAME generated_zwetschge_flash_image_matches_example
    COMMAND "${CMAKE_COMMAND}" -E compare_files "${CMAKE_CURRENT_SOURCE_DIR}/data/prebuilt_copy_of_exampleFlashImage.zwetschge" "${ROYALE_RUNTIME_OUTPUT_DIRECTORY}/exampleFlashImage.zwetschge"
    )
add_dependencies(comptests_storage
    exampleZwetschgeDeviceTarget
    exampleZwetschgeFlashImageTarget
    )
set_target_properties(exampleZwetschgeDeviceTarget
    PROPERTIES
    FOLDER components/tests
    EXCLUDE_FROM_ALL true
    )
set_target_properties(exampleZwetschgeFlashImageTarget
    PROPERTIES
    FOLDER components/tests
    EXCLUDE_FROM_ALL true
    )
